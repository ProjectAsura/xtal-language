
lib::test: singleton{
	Test: "Test";
	Before: "Before";
	After: "After";
	
	_ok;
	_fail;
	
	run_files: fun(files){
		_ok = 0;
		_fail = 0;
		files{
			run_file(it);
		}
		
		printf("ok=%d, fail=%d\n", _ok, _fail);
	}
	
	run_file: fun(file){
		file.p;
		debug::scope{
			printf("file %s\n", file);
			
			code: compile_file(file);
			code();
			
			code.filelocal.members{ |name, name2, mem|
				if(mem is Class){
					tests: [];
					befores: [];
					afters: [];
					
					mem.members{ |name, name2, mem|
						if(name2 === Test){ tests.push_back(name); }
						if(name2 === Before){ befores.push_back(name); }
						if(name2 === After){ afters.push_back(name); }
					}
					
					if(!tests.is_empty){
						obj: mem();
						tests{
							try{
								befores{ obj.(it)#Before; }
								obj.(it)#Test;
								afters{ obj.(it)#After; }
								printf("\tok %s\n", it);
								_ok++;
							}
							catch(e){
								printf("\tfail %s:%s\n", it, e);
								_fail++;
							}
						}
					}
				}
			}
		}
	}
	
	run_dir: fun(dir){					
		run_files(filesystem::entries(dir)
					.select(|x| x.match(once ".xtal" >> xpeg::eos))
					.map(|x| dir ~ "/" ~ x)
			);
	}
}
